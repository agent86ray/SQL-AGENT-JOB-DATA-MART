CREATE PROCEDURE [dbo].[CALCULATE_JOB_STEP_AVERAGE_DURATION]
	@BEGIN_JOB_INSTANCE INT
,	@END_JOB_INSTANCE	INT
AS
BEGIN
	/*
		Calculate the average duration for the job steps based on the @BEGIN_JOB_INSTANCE 
		and @END_JOB_INSTANCE range.
	*/

	TRUNCATE TABLE [dbo].[JOB_STEP_AVERAGE_DURATION_UPDATE];

	;WITH CTE_JOBS AS (
		SELECT DISTINCT
			[JOB_KEY]
		FROM [dbo].[JOB_STEP_INSTANCE]
		WHERE [JOB_STEP_INSTANCE_ID] BETWEEN @BEGIN_JOB_INSTANCE AND @END_JOB_INSTANCE
	)
	
	, CTE_SUM_DURATION AS (
		SELECT 
			d.[JOB_KEY]					
		,	d.[step_id]					
		,	SUM(d.[JOB_STEP_COUNT])			[TOTAL_STEP_COUNT]
		,	SUM(d.[JOB_STEP_TOTAL_SECONDS])	[TOTAL_STEP_DURATION]
		FROM [dbo].[JOB_STEP_DURATION] d
		JOIN CTE_JOBS j
		ON j.[JOB_KEY] = d.[JOB_KEY]
		GROUP BY d.[JOB_KEY], d.[step_id]
	)

	, CTE_AVG_DURATION AS (
		SELECT
			[JOB_KEY]					
		,	[step_id]					
		,	[TOTAL_STEP_COUNT]
		,	[TOTAL_STEP_DURATION]
		,	IIF(ISNULL([TOTAL_STEP_COUNT], 0) > 0
			,	[TOTAL_STEP_DURATION] / [TOTAL_STEP_COUNT]
			,	NULL
			)	AS [AVERAGE_STEP_DURATION]
		FROM CTE_SUM_DURATION	
	)

	INSERT [dbo].[JOB_STEP_AVERAGE_DURATION_UPDATE] (
		[JOB_KEY]					
	,	[step_id]					
	,	[TOTAL_STEP_COUNT]			
	,	[TOTAL_STEP_DURATION]		
	,	[AVERAGE_STEP_DURATION]		
	)

	SELECT
		[JOB_KEY]					
	,	[step_id]					
	,	[TOTAL_STEP_COUNT]			
	,	[TOTAL_STEP_DURATION]		
	,	[AVERAGE_STEP_DURATION]		
	FROM CTE_AVG_DURATION;

	-- update existing average durations
	UPDATE d
	SET
		[TOTAL_STEP_COUNT] = u.[TOTAL_STEP_COUNT]
	,	[TOTAL_STEP_DURATION] = u.[TOTAL_STEP_DURATION]
	,	[AVERAGE_STEP_DURATION]	= u.[AVERAGE_STEP_DURATION]	
	FROM [dbo].[JOB_STEP_AVERAGE_DURATION] d
	JOIN [dbo].[JOB_STEP_AVERAGE_DURATION_UPDATE] u
	ON u.[JOB_KEY] = d.[JOB_KEY] AND u.[step_id] = d.[step_id]

	-- insert new average durations
	INSERT [dbo].[JOB_STEP_AVERAGE_DURATION] (
		[JOB_KEY]					
	,	[step_id]					
	,	[TOTAL_STEP_COUNT]			
	,	[TOTAL_STEP_DURATION]		
	,	[AVERAGE_STEP_DURATION]		
	)

	SELECT
		u.[JOB_KEY]					
	,	u.[step_id]					
	,	u.[TOTAL_STEP_COUNT]			
	,	u.[TOTAL_STEP_DURATION]		
	,	u.[AVERAGE_STEP_DURATION]		
	FROM [dbo].[JOB_STEP_AVERAGE_DURATION_UPDATE] u
	LEFT JOIN [dbo].[JOB_STEP_AVERAGE_DURATION] d
	ON d.[JOB_KEY] = u.[JOB_KEY] AND d.[step_id] = u.[step_id]
	WHERE d.[JOB_KEY] IS NULL;
END
